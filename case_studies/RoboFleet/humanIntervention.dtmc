// Human intervention model

// Aim:
// This model is used to obtained the expected cost to be paid
// for a suppervisor in charge of fixing a series of robots
// everytime any gets stock in their activities.
// Rmax=?[F done]

// Model description:
// The model describes a supervisor, worker @w, intervining when an error is detected.
// The probability of error depends on the independent models of each robot.

dtmc

formula done = w=3 | w=4;

//--prob. of failing with their activities, from each robot's model.
const double p_r1 = 0.31640625;
const double p_r2 = 0.0;
//--prob. human intervention solved the problem
const double p_fix1=0.8;
const double p_fix2=0.9;

module supervisor1
 w:[0..4] init 0;
 //w=0 - robots working fine, no human intervention
 //w=1 - robot failure detected, human intervene
 //w=2 - robot reset after first human intervention failed - more costly as require hard stop
 //w=3 - done successfully
 //w=4 - fail
 [ ]  w=0 -> (p_r1+p_r2-p_r1*p_r2):(w'=1) + (1-((p_r1+p_r2-p_r1*p_r2))):(w'=3);
 [interveneAfterFail] w=1 -> p_fix1:(w'=3) + 1-p_fix1:(w'=2);
 [hardReset] w=2 -> p_fix2:(w'=3) + 1-p_fix2:(w'=4);
 [succ] w=3 -> (w'=3);
 [fail] w=4 -> (w'=4);

endmodule


rewards "intervation_cost"
 w=1 : 1;
 w=2 : 3;
endrewards
