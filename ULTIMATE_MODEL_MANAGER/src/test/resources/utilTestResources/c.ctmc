ctmc

//
// Model of garnment picking by the robotic arm
//

// Model parameters
// Dependency parameters (obtained through the analysis of other models)
// - no dependency parameter
// Environment parameters (derived from data collected from observing the environment)
const double rPick;        // garnment picking rate
const double psucc;        // probability of successful picking the garnment when attempting to do it
// Internal parameters (configurable to meet assumptions about the modelled system aspect, or to comply with requirements)
const int maxRetries;      // maximum number of retries if the garnment picking is unsuccessful

// Labels
label "done" = s=1 | tries=maxRetries;
label "succ" = s=1;

module pickGarment
 s : [0..1] init 0;              // 0 - initial/try state/give up, 1 - success
 tries : [0..maxRetries] init 0; // tries counter
 
 [try]    s=0 & tries<maxRetries -> psucc*rPick:(s'=1) + (1-psucc)*rPick:(tries'=tries+1); // try to pick garnment
 [giveUp] s=0 & tries=maxRetries -> true;                                                  // give up after maxRetries unsuccessful attempts

 [succes] s=1 -> true;                                                                     // succeeeded
endmodule

rewards "energy"
  [try] true : 30;  // energy used for each garnment picking attempt
endrewards